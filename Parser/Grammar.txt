START -> @1 PROGRAM  .

PROGRAM -> @1 CLASSDECLARATIONS @2 FUNCTIONDEFINITIONS @2 main FUNCTIONBODY @2 @2  .

CLASSDECLARATIONS -> @1 CLASSDECLARATION @2 CLASSDECLARATIONS @3  .
CLASSDECLARATIONS ->   .

CLASSDECLARATION -> class id @2 OPTIONALINHERITS @2 lcurbr MEMBERDECLARATIONS @2 rcurbr semi  .

FUNCTIONDEFINITIONS -> @1 FUNCTIONDEFINITION @2 FUNCTIONDEFINITIONS @3  .
FUNCTIONDEFINITIONS ->   .

FUNCTIONDEFINITION -> @1 FUNCTIONSIGNATURE @2 FUNCTIONBODY @2  .

MEMBERDECLARATIONS -> @1 VISIBILITY MEMBERDECLARATION @4 @2 MEMBERDECLARATIONS @3  .
MEMBERDECLARATIONS ->   .

MEMBERDECLARATION -> id FUNCTIONORVARIABLEDECLARATION @4  .
MEMBERDECLARATION -> TYPENONID VARIABLEDECLARATION @4  .

FUNCTIONORVARIABLEDECLARATION -> FUNCTIONDECLARATION  .
FUNCTIONORVARIABLEDECLARATION -> VARIABLEDECLARATION  .

VISIBILITY -> public  .
VISIBILITY -> private  .

STATEMENT -> if lpar RELEXPRESSION @2_#condition rpar then STATEMENTBLOCK @2_#statement_body else STATEMENTBLOCK @2_#statement_body semi  .
STATEMENT -> while lpar RELEXPRESSION @2_#condition rpar STATEMENTBLOCK @2_#statement_body semi  .
STATEMENT -> read lpar @1_#var STATEMENTVARIABLE @2 rpar semi  .
STATEMENT -> write lpar EXPRESSION @2 rpar semi  .
STATEMENT -> return lpar EXPRESSION @2 rpar semi  .
STATEMENT -> ASSIGNSTATEMENTORFUNCTIONCALL  .

STATEMENTVARIABLE -> @1_#variables id STATEMENTVARIABLEORFUNCTIONCALL  .

STATEMENTVARIABLEORFUNCTIONCALL -> @1_#variable @4 INDICES @2 @2 STATEMENTVARIABLEEXT  .
STATEMENTVARIABLEORFUNCTIONCALL -> @1_#func_call @4 lpar FUNCTIONCALLPARAMS @2 @2 rpar STATEMENTFUNCTIONCALL  .

STATEMENTVARIABLEEXT -> dot STATEMENTVARIABLE  .
STATEMENTVARIABLEEXT ->   .

STATEMENTFUNCTIONCALL -> dot STATEMENTVARIABLE  .

ASSIGNSTATEMENTORFUNCTIONCALL -> @1_#variables id VARIABLEORFUNCTIONCALLEXT  .

VARIABLEORFUNCTIONCALLEXT -> @1_#variable @4 INDICES @2 @2 VARIABLEEXT  .
VARIABLEORFUNCTIONCALLEXT -> @1_#func_call lpar FUNCTIONCALLPARAMS @2 @2 rpar FUNCTIONCALLEXT  .

VARIABLEEXT -> ASSIGNMENTOP @4 EXPRESSION @2 semi  .
VARIABLEEXT -> dot ASSIGNSTATEMENTORFUNCTIONCALL  .

FUNCTIONCALLEXT -> @1_#func_call_chain @4 semi  .
FUNCTIONCALLEXT -> dot ASSIGNSTATEMENTORFUNCTIONCALL  .

FUNCTIONPARAMS -> @1 @1_#param TYPE @2 id ARRAYDIMENSIONS @2 @2 @2 FUNCTIONPARAMSTAILS  .
FUNCTIONPARAMS ->   .

ADDOP -> plus  .
ADDOP -> minus  .
ADDOP -> or  .

OPTIONALINHERITS -> @1 inherits id @2 INHERITEDCLASSES @3  .
OPTIONALINHERITS ->   .

RELEXPRESSION -> @1 ARITHEXPRESSION COMPAREOP @4 ARITHEXPRESSION @2  .

FUNCTIONDECLARATION -> @1 lpar FUNCTIONPARAMS @2 rpar colon TYPEORVOID @2_#return_type semi  .

FUNCTIONCALLPARAMSTAILS -> @1 FUNCTIONCALLPARAMSTAIL @2 FUNCTIONCALLPARAMSTAILS @3  .
FUNCTIONCALLPARAMSTAILS ->   .

LOCALSCOPE -> local VARIABLEDECLARATIONS @2 @2_#local_variables  .
LOCALSCOPE ->   .

ARRAYDIMENSIONS -> @1 ARRAYSIZE @2 ARRAYDIMENSIONS @3  .
ARRAYDIMENSIONS ->   .

EXPRESSION -> @1 ARITHEXPRESSION RELEXPRESSIONORNULL @4  .

RELEXPRESSIONORNULL -> COMPAREOP ARITHEXPRESSION @2  .
RELEXPRESSIONORNULL ->   .

STATEMENTS -> @1 STATEMENT @2 STATEMENTS @3  .
STATEMENTS ->   .

ARITHEXPRESSION -> TERM RIGHTRECARITHEXPRESSION  .

RIGHTRECARITHEXPRESSION ->   .
RIGHTRECARITHEXPRESSION -> ADDOP TERM RIGHTRECARITHEXPRESSION  .

FUNCTIONSIGNATURE -> id FUNCTIONSIGNATURENAMESPACE  .

FUNCTIONSIGNATURENAMESPACE -> FUNCTIONSIGNATUREEXT  .
FUNCTIONSIGNATURENAMESPACE -> coloncolon id FUNCTIONSIGNATUREEXT  .

FUNCTIONSIGNATUREEXT -> lpar FUNCTIONPARAMS rpar colon TYPEORVOID  .

FUNCTIONPARAMSTAILS -> FUNCTIONPARAMSTAIL FUNCTIONPARAMSTAILS  .
FUNCTIONPARAMSTAILS ->   .

INHERITEDCLASSES -> @1 comma id @2 INHERITEDCLASSES @3  .
INHERITEDCLASSES ->   .

SIGN -> plus  .
SIGN -> minus  .

COMPAREOP -> eq  .
COMPAREOP -> noteq  .
COMPAREOP -> less  .
COMPAREOP -> greater  .
COMPAREOP -> lesseq  .
COMPAREOP -> greatereq  .

INDEX -> @1 lsqbr ARITHEXPRESSION @2 rsqbr  .

VARIABLEDECLARATIONS -> @1 TYPE VARIABLEDECLARATION @4 @2 VARIABLEDECLARATIONS @3  .
VARIABLEDECLARATIONS ->   .

FACTOR -> VARIABLEFUNCTIONCALL  .
FACTOR -> intnum  .
FACTOR -> floatnum  .
FACTOR -> lpar ARITHEXPRESSION rpar  .
FACTOR -> not FACTOR  .
FACTOR -> SIGN FACTOR  .

VARIABLEFUNCTIONCALL -> id VARIABLEORFUNCTIONCALL  .

VARIABLEORFUNCTIONCALL -> INDICES FACTORVARIABLE  .
VARIABLEORFUNCTIONCALL -> lpar FUNCTIONCALLPARAMS rpar FACTORFUNCTIONCALL  .

FACTORVARIABLE -> dot VARIABLEFUNCTIONCALL  .
FACTORVARIABLE ->   .

FACTORFUNCTIONCALL -> dot VARIABLEFUNCTIONCALL  .
FACTORFUNCTIONCALL ->   .

TERM -> FACTOR RIGHTRECTERM  .

MULTOP -> mult  .
MULTOP -> div  .
MULTOP -> and  .

RIGHTRECTERM ->   .
RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM  .

TYPEORVOID -> TYPE  .
TYPEORVOID -> void  .

TYPE -> TYPENONID  .
TYPE -> id  .

TYPENONID -> integer  .
TYPENONID -> float  .

ARRAYSIZE -> lsqbr OPTIONALINT rsqbr  .

OPTIONALINT -> intnum  .
OPTIONALINT ->   .

FUNCTIONCALLPARAMSTAIL -> comma EXPRESSION  .

FUNCTIONCALLPARAMS -> EXPRESSION FUNCTIONCALLPARAMSTAILS  .
FUNCTIONCALLPARAMS ->   .

VARIABLEDECLARATION -> id ARRAYDIMENSIONS semi  .

FUNCTIONBODY -> LOCALSCOPE do STATEMENTS end  .

STATEMENTBLOCK -> do STATEMENTS end  .
STATEMENTBLOCK -> STATEMENT  .
STATEMENTBLOCK ->   .

ASSIGNMENTOP -> equal  .

FUNCTIONPARAMSTAIL -> comma TYPE id ARRAYDIMENSIONS  .

INDICES -> INDEX INDICES  .
INDICES ->   .


