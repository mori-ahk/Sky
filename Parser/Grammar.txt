START -> PROGRAM  .

PROGRAM -> CLASSDECLARATIONS FUNCTIONDEFINITIONS main FUNCTIONBODY  .

CLASSDECLARATIONS -> CLASSDECLARATION CLASSDECLARATIONS  .
CLASSDECLARATIONS ->   .

CLASSDECLARATION -> class id OPTIONALINHERITS lcurbr MEMBERDECLARATIONS rcurbr semi  .

FUNCTIONDEFINITIONS -> FUNCTIONDEFINITION FUNCTIONDEFINITIONS  .
FUNCTIONDEFINITIONS ->   .

FUNCTIONDEFINITION -> FUNCTIONSIGNATURE FUNCTIONBODY  .

MEMBERDECLARATIONS -> VISIBILITY MEMBERDECLARATION MEMBERDECLARATIONS  .
MEMBERDECLARATIONS ->   .

MEMBERDECLARATION -> id FUNCTIONORVARIABLEDECLARATION  .
MEMBERDECLARATION -> TYPENONID VARIABLEDECLARATION  .

FUNCTIONORVARIABLEDECLARATION -> FUNCTIONDECLARATION  .
FUNCTIONORVARIABLEDECLARATION -> VARIABLEDECLARATION  .

VISIBILITY -> public  .
VISIBILITY -> private  .

STATEMENT -> if lpar RELEXPRESSION rpar then STATEMENTBLOCK else STATEMENTBLOCK semi  .
STATEMENT -> while lpar RELEXPRESSION rpar STATEMENTBLOCK semi  .
STATEMENT -> read lpar STATEMENTVARIABLE rpar semi  .
STATEMENT -> write lpar EXPRESSION rpar semi  .
STATEMENT -> return lpar EXPRESSION rpar semi  .
STATEMENT -> ASSIGNSTATEMENTORFUNCTIONCALL  .

STATEMENTVARIABLE -> id STATEMENTVARIABLEORFUNCTIONCALL  .

STATEMENTVARIABLEORFUNCTIONCALL -> INDICES STATEMENTVARIABLEEXT  .
STATEMENTVARIABLEORFUNCTIONCALL -> lpar FUNCTIONCALLPARAMS rpar STATEMENTFUNCTIONCALL  .

STATEMENTVARIABLEEXT -> dot STATEMENTVARIABLE  .
STATEMENTVARIABLEEXT ->   .

STATEMENTFUNCTIONCALL -> dot STATEMENTVARIABLE  .

ASSIGNSTATEMENTORFUNCTIONCALL -> id VARIABLEORFUNCTIONCALLEXT  .

VARIABLEORFUNCTIONCALLEXT -> INDICES VARIABLEEXT  .
VARIABLEORFUNCTIONCALLEXT -> lpar FUNCTIONCALLPARAMS rpar FUNCTIONCALLEXT  .

VARIABLEEXT -> ASSIGNMENTOP EXPRESSION semi  .
VARIABLEEXT -> dot ASSIGNSTATEMENTORFUNCTIONCALL  .

FUNCTIONCALLEXT -> semi  .
FUNCTIONCALLEXT -> dot ASSIGNSTATEMENTORFUNCTIONCALL  .

FUNCTIONPARAMS -> TYPE id ARRAYDIMENSIONS FUNCTIONPARAMSTAILS  .
FUNCTIONPARAMS ->   .

ADDOP -> plus  .
ADDOP -> minus  .
ADDOP -> or  .

OPTIONALINHERITS -> inherits id INHERITEDCLASSES  .
OPTIONALINHERITS ->   .

RELEXPRESSION -> ARITHEXPRESSION COMPAREOP ARITHEXPRESSION  .

FUNCTIONDECLARATION -> lpar FUNCTIONPARAMS rpar colon TYPEORVOID semi  .

FUNCTIONCALLPARAMSTAILS -> FUNCTIONCALLPARAMSTAIL FUNCTIONCALLPARAMSTAILS  .
FUNCTIONCALLPARAMSTAILS ->   .

LOCALSCOPE -> local VARIABLEDECLARATIONS  .
LOCALSCOPE ->   .

ARRAYDIMENSIONS -> ARRAYSIZE ARRAYDIMENSIONS  .
ARRAYDIMENSIONS ->   .

EXPRESSION -> ARITHEXPRESSION RELEXPRESSIONORNULL  .

RELEXPRESSIONORNULL -> COMPAREOP ARITHEXPRESSION  .
RELEXPRESSIONORNULL ->   .

STATEMENTS -> STATEMENT STATEMENTS  .
STATEMENTS ->   .

ARITHEXPRESSION -> TERM RIGHTRECARITHEXPRESSION  .

RIGHTRECARITHEXPRESSION ->   .
RIGHTRECARITHEXPRESSION -> ADDOP TERM RIGHTRECARITHEXPRESSION  .

FUNCTIONSIGNATURE -> id FUNCTIONSIGNATURENAMESPACE  .

FUNCTIONSIGNATURENAMESPACE -> FUNCTIONSIGNATUREEXT  .
FUNCTIONSIGNATURENAMESPACE -> coloncolon id FUNCTIONSIGNATUREEXT  .

FUNCTIONSIGNATUREEXT -> lpar FUNCTIONPARAMS rpar colon TYPEORVOID  .

FUNCTIONPARAMSTAILS -> FUNCTIONPARAMSTAIL FUNCTIONPARAMSTAILS  .
FUNCTIONPARAMSTAILS ->   .

INHERITEDCLASSES -> comma id INHERITEDCLASSES  .
INHERITEDCLASSES ->   .

SIGN -> plus  .
SIGN -> minus  .

COMPAREOP -> eq  .
COMPAREOP -> noteq  .
COMPAREOP -> less  .
COMPAREOP -> greater  .
COMPAREOP -> lesseq  .
COMPAREOP -> greatereq  .

INDEX -> lsqbr ARITHEXPRESSION rsqbr  .

VARIABLEDECLARATIONS -> TYPE VARIABLEDECLARATION VARIABLEDECLARATIONS  .
VARIABLEDECLARATIONS ->   .

FACTOR -> VARIABLEFUNCTIONCALL  .
FACTOR -> intnum  .
FACTOR -> floatnum  .
FACTOR -> lpar ARITHEXPRESSION rpar  .
FACTOR -> not FACTOR  .
FACTOR -> SIGN FACTOR  .

VARIABLEFUNCTIONCALL -> id VARIABLEORFUNCTIONCALL  .

VARIABLEORFUNCTIONCALL -> INDICES FACTORVARIABLE  .
VARIABLEORFUNCTIONCALL -> lpar FUNCTIONCALLPARAMS rpar FACTORFUNCTIONCALL  .

FACTORVARIABLE -> dot VARIABLEFUNCTIONCALL  .
FACTORVARIABLE ->   .

FACTORFUNCTIONCALL -> dot VARIABLEFUNCTIONCALL  .
FACTORFUNCTIONCALL ->   .

TERM -> FACTOR RIGHTRECTERM  .

MULTOP -> mult  .
MULTOP -> div  .
MULTOP -> and  .

RIGHTRECTERM ->   .
RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM  .

TYPEORVOID -> TYPE  .
TYPEORVOID -> void  .

TYPE -> TYPENONID  .
TYPE -> id  .

TYPENONID -> integer  .
TYPENONID -> float  .

ARRAYSIZE -> lsqbr OPTIONALINT rsqbr  .

OPTIONALINT -> intnum  .
OPTIONALINT ->   .

FUNCTIONCALLPARAMSTAIL -> comma EXPRESSION  .

FUNCTIONCALLPARAMS -> EXPRESSION FUNCTIONCALLPARAMSTAILS  .
FUNCTIONCALLPARAMS ->   .

VARIABLEDECLARATION -> id ARRAYDIMENSIONS semi  .

FUNCTIONBODY -> LOCALSCOPE do STATEMENTS end  .

STATEMENTBLOCK -> do STATEMENTS end  .
STATEMENTBLOCK -> STATEMENT  .
STATEMENTBLOCK ->   .

ASSIGNMENTOP -> equal  .

FUNCTIONPARAMSTAIL -> comma TYPE id ARRAYDIMENSIONS  .

INDICES -> INDEX INDICES  .
INDICES ->   .


