START -> PROGRAM  .

PROGRAM -> CLASS_DECLARATIONS FUNCTION_DEFINITIONS main FUNCTION_BODY  .

CLASS_DECLARATIONS -> CLASS_DECLARATION CLASS_DECLARATIONS  .
CLASS_DECLARATIONS ->   .

CLASS_DECLARATION -> class id OPTIONAL_INHERITS lcurbr MEMBER_DECLARATIONS rcurbr semi  .

FUNCTION_DEFINITIONS -> FUNCTION_DEFINITION FUNCTION_DEFINITIONS  .
FUNCTION_DEFINITIONS ->   .

FUNCTION_DEFINITION -> FUNCTION_SIGNATURE FUNCTION_BODY  .

MEMBER_DECLARATIONS -> VISIBILITY MEMBER_DECLARATION MEMBER_DECLARATIONS  .
MEMBER_DECLARATIONS ->   .

MEMBER_DECLARATION -> id FUNCTION_OR_VARIABLE_DECLARATION  .
MEMBER_DECLARATION -> TYPE_NON_ID VARIABLE_DECLARATION  .

FUNCTION_OR_VARIABLE_DECLARATION -> FUNCTION_DECLARATION  .
FUNCTION_OR_VARIABLE_DECLARATION -> VARIABLE_DECLARATION  .

VISIBILITY -> public  .
VISIBILITY -> private  .

STATEMENT -> if lpar REL_EXPRESSION rpar then STATEMENT_BLOCK else STATEMENT_BLOCK semi  .
STATEMENT -> while lpar REL_EXPRESSION rpar STATEMENT_BLOCK semi  .
STATEMENT -> read lpar STATEMENT_VARIABLE rpar semi  .
STATEMENT -> write lpar EXPRESSION rpar semi  .
STATEMENT -> return lpar EXPRESSION rpar semi  .
STATEMENT -> ASSIGN_STATEMENT_OR_FUNCTION_CALL  .

STATEMENT_VARIABLE -> id STATEMENT_VARIABLE_OR_FUNCTION_CALL  .

STATEMENT_VARIABLE_OR_FUNCTION_CALL -> INDICES STATEMENT_VARIABLE_EXT  .
STATEMENT_VARIABLE_OR_FUNCTION_CALL -> lpar FUNCTION_CALL_PARAMS rpar STATEMENT_FUNCTION_CALL  .

STATEMENT_VARIABLE_EXT -> dot STATEMENT_VARIABLE  .
STATEMENT_VARIABLE_EXT ->   .

STATEMENT_FUNCTION_CALL -> dot STATEMENT_VARIABLE  .

ASSIGN_STATEMENT_OR_FUNCTION_CALL -> id VARIABLE_OR_FUNCTION_CALL_EXT  .

VARIABLE_OR_FUNCTION_CALL_EXT -> INDICES VARIABLE_EXT  .
VARIABLE_OR_FUNCTION_CALL_EXT -> lpar FUNCTION_CALL_PARAMS rpar FUNCTION_CALL_EXT  .

VARIABLE_EXT -> ASSIGNMENT_OP EXPRESSION semi  .
VARIABLE_EXT -> dot ASSIGN_STATEMENT_OR_FUNCTION_CALL  .

FUNCTION_CALL_EXT -> semi  .
FUNCTION_CALL_EXT -> dot ASSIGN_STATEMENT_OR_FUNCTION_CALL  .

FUNCTION_PARAMS -> TYPE id ARRAY_DIMENSIONS FUNCTION_PARAMS_TAILS  .
FUNCTION_PARAMS ->   .

ADD_OP -> plus  .
ADD_OP -> minus  .
ADD_OP -> or  .

OPTIONAL_INHERITS -> inherits id INHERITED_CLASSES  .
OPTIONAL_INHERITS ->   .

REL_EXPRESSION -> ARITH_EXPRESSION COMPARE_OP ARITH_EXPRESSION  .

FUNCTION_DECLARATION -> lpar FUNCTION_PARAMS rpar colon TYPE_OR_VOID semi  .

FUNCTION_CALL_PARAMS_TAILS -> FUNCTION_CALL_PARAMS_TAIL FUNCTION_CALL_PARAMS_TAILS  .
FUNCTION_CALL_PARAMS_TAILS ->   .

LOCAL_SCOPE -> local VARIABLE_DECLARATIONS  .
LOCAL_SCOPE ->   .

ARRAY_DIMENSIONS -> ARRAY_SIZE ARRAY_DIMENSIONS  .
ARRAY_DIMENSIONS ->   .

EXPRESSION -> ARITH_EXPRESSION  REL_EXPRESSION_OR_NULL  .

REL_EXPRESSION_OR_NULL -> COMPARE_OP ARITH_EXPRESSION  .
REL_EXPRESSION_OR_NULL ->   .

STATEMENTS -> STATEMENT STATEMENTS  .
STATEMENTS ->   .

ARITH_EXPRESSION -> TERM RIGHT_REC_ARITH_EXPRESSION  .

RIGHT_REC_ARITH_EXPRESSION ->   .
RIGHT_REC_ARITH_EXPRESSION -> ADD_OP TERM RIGHT_REC_ARITH_EXPRESSION  .

FUNCTION_SIGNATURE -> id FUNCTION_SIGNATURE_NAMESPACE  .

FUNCTION_SIGNATURE_NAMESPACE -> FUNCTION_SIGNATURE_EXT  .
FUNCTION_SIGNATURE_NAMESPACE -> coloncolon id FUNCTION_SIGNATURE_EXT  .

FUNCTION_SIGNATURE_EXT -> lpar FUNCTION_PARAMS rpar colon TYPE_OR_VOID  .

FUNCTION_PARAMS_TAILS -> FUNCTION_PARAMS_TAIL FUNCTION_PARAMS_TAILS  .
FUNCTION_PARAMS_TAILS ->   .

INHERITED_CLASSES -> comma id INHERITED_CLASSES  .
INHERITED_CLASSES ->   .

SIGN -> plus  .
SIGN -> minus  .

COMPARE_OP -> eq  .
COMPARE_OP -> noteq  .
COMPARE_OP -> less  .
COMPARE_OP -> greater  .
COMPARE_OP -> lesseq  .
COMPARE_OP -> greatereq  .

INDEX -> lsqbr ARITH_EXPRESSION rsqbr  .

VARIABLE_DECLARATIONS -> TYPE VARIABLE_DECLARATION VARIABLE_DECLARATIONS  .
VARIABLE_DECLARATIONS ->   .

FACTOR -> VARIABLE_FUNCTION_CALL  .
FACTOR -> intnum  .
FACTOR -> floatnum  .
FACTOR -> lpar ARITH_EXPRESSION rpar  .
FACTOR -> not FACTOR  .
FACTOR -> SIGN FACTOR  .

VARIABLE_FUNCTION_CALL -> id VARIABLE_OR_FUNCTION_CALL  .

VARIABLE_OR_FUNCTION_CALL -> INDICES FACTOR_VARIABLE  .
VARIABLE_OR_FUNCTION_CALL -> lpar FUNCTION_CALL_PARAMS rpar FACTOR_FUNCTION_CALL  .

FACTOR_VARIABLE -> dot VARIABLE_FUNCTION_CALL  .
FACTOR_VARIABLE ->   .

FACTOR_FUNCTION_CALL -> dot VARIABLE_FUNCTION_CALL  .
FACTOR_FUNCTION_CALL ->   .

TERM -> FACTOR RIGHT_REC_TERM  .

MULT_OP -> mult  .
MULT_OP -> div  .
MULT_OP -> and  .

RIGHT_REC_TERM ->   .
RIGHT_REC_TERM -> MULT_OP FACTOR RIGHT_REC_TERM  .

TYPE_OR_VOID -> TYPE  .
TYPE_OR_VOID -> void  .

TYPE -> TYPE_NON_ID  .
TYPE -> id  .

TYPE_NON_ID -> integer  .
TYPE_NON_ID -> float  .

ARRAY_SIZE -> lsqbr OPTIONAL_INT rsqbr  .

OPTIONAL_INT -> intnum  .
OPTIONAL_INT ->   .

FUNCTION_CALL_PARAMS_TAIL -> comma EXPRESSION  .

FUNCTION_CALL_PARAMS -> EXPRESSION FUNCTION_CALL_PARAMS_TAILS  .
FUNCTION_CALL_PARAMS ->   .

VARIABLE_DECLARATION -> id ARRAY_DIMENSIONS semi  .

FUNCTION_BODY -> LOCAL_SCOPE do STATEMENTS end  .

STATEMENT_BLOCK -> do STATEMENTS end  .
STATEMENT_BLOCK -> STATEMENT  .
STATEMENT_BLOCK ->   .

ASSIGNMENT_OP -> equal  .

FUNCTION_PARAMS_TAIL -> comma TYPE id ARRAY_DIMENSIONS  .

INDICES -> INDEX INDICES  .
INDICES ->   .